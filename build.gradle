
plugins {
    id 'nebula.plugin-plugin' version '16.2.0'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation gradleApi()
    testImplementation 'cglib:cglib-nodep:3.2.4'
    testImplementation 'org.apache.commons:commons-io:1.3.2'
    testImplementation 'com.github.stefanbirkner:system-rules:1.19.0'
}

contacts {
    'nebula-plugins-oss@netflix.com' {
        moniker 'Nebula Plugins Maintainers'
        github 'nebula-plugins'
    }
}

gradlePlugin {
    plugins {
        nebulaGrunt {
            id = 'nebula.grunt'
            implementationClass = 'com.moowork.gradle.grunt.GruntPlugin'
        }

        nebulaGulp {
            id = 'nebula.gulp'
            implementationClass = 'com.moowork.gradle.gulp.GulpPlugin'
        }

        nebulaNode {
            id = 'nebula.node'
            implementationClass = 'com.moowork.gradle.node.NodePlugin'
        }

    }
}

description = 'Plugins to ease use of Node tools (Node, Grunt, Gulp)'

pluginBundle {
    website = 'https://github.com/nebula-plugins/nebula-node-plugin'
    vcsUrl = 'https://github.com/nebula-plugins/nebula-node-plugin.git'
    description = 'Plugins to ease use of Node tools (Node, Grunt, Gulp)'

    plugins {
        nebulaGrunt {
            id = 'nebula.grunt'
            displayName = 'Nebula Grunt Plugin'
            description = 'Nebula Grunt Plugin'
            tags = ['nebula']
        }
        nebulaGulp {
            id = 'nebula.gulp'
            displayName = 'Nebula Gulp Plugin'
            description = 'Nebula Gulp Plugin'
            tags = ['nebula']
        }
        nebulaNode {
            id = 'nebula.node'
            displayName = 'Nebula Node Plugin'
            description = 'Nebula Node Plugin'
            tags = ['nebula']
        }
    }
}

tasks.withType(Test) {
    if (JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_17)) {
        // Necessary to run system-rules that uses reflection to modify system properties and/or env variables
        jvmArgs("--add-opens", "java.base/java.util=ALL-UNNAMED", "--add-opens", "java.base/java.lang=ALL-UNNAMED")
    }
}
